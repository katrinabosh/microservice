buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
}


plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.1.1.RELEASE'

    id "checkstyle"
    id "pmd"
    id "com.github.spotbugs" version "1.6.9"
    id "jacoco"
    id "info.solidsoft.pitest" version "1.4.0"
    
}

group 'ru.test'
version '1.0-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url = 'http://packages.confluent.io/maven/'
    }
    
}

dependencies {

    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// чтобы идея подхватывала настройки сервиса
idea { 
    project {
        sourceCompatibility = 11
        languageLevel = 11
    }
}

// отвечает за создание исполняемого файла jar
bootJar {
    baseName = "test-micro" // имя файла
}

checkstyle {
    toolVersion = '8.21'
}

spotbugs {
    toolVersion = '3.1.12'
}

pmd {
    toolVersion = '6.15.0'
}

jacoco {
    toolVersion = '0.8.4'
}

pitest {
    pitestVersion = '1.4.8'
}


tasks.withType(Checkstyle) {
    maxWarnings = 0
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    excludeFilter rootProject.file('config/spotbugs/spotbugs.xml')
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(JacocoReport) {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.destination file("${buildDir}/reports/coverage")
    }
}